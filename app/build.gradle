apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

def gitCommitHash = 'git rev-parse --verify --short HEAD'.execute().text.trim()
def tagName = '0.9_beta'

boolean signConfigExists = project.hasProperty("keystoreFile") && project.hasProperty("keystorePassword") && project.hasProperty("keystoreAlias") && project.hasProperty("keystoreAliasPassword")

android {
    if (signConfigExists) {
        signingConfigs {
            release {
                storeFile file(project.property("keystoreFile"))
                storePassword project.property("keystorePassword")
                keyAlias project.property("keystoreAlias")
                keyPassword project.property("keystoreAliasPassword")
            }
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.kieronquinn.app.taptap"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 9
        
        versionName "0.9 Beta"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        buildConfigField("String", "GIT_HASH", "\"${gitCommitHash}\"")
        buildConfigField("String", "TAG_NAME", "\"${tagName}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (signConfigExists) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    aaptOptions {
        noCompress "tflite", 'so'
    }
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //AndroidX
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha08'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    implementation "androidx.lifecycle:lifecycle-service:2.2.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.work:work-runtime-ktx:2.4.0"
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.browser:browser:1.2.0'
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0"

    //Core Library Desugaring: https://developer.android.com/studio/write/java8-support#library-desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    //Google
    kapt 'com.google.dagger:dagger-compiler:2.28.3'
    implementation files('libs/columbus.jar')
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.dagger:dagger:2.28.3'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.tensorflow:tensorflow-lite:0.0.0-nightly'

    //Third Party
    implementation "dev.chrisbanes:insetter-ktx:0.3.0"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.joaomgcd:taskerpluginlibrary:0.3.2'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.3.0'
    implementation "com.airbnb.android:lottie:3.4.0"
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.19'
    implementation 'me.saket:better-link-movement-method:2.2.0'
    implementation 'com.github.judemanutd:autostarter:1.0.8'

    //Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
